import static com.github.zhurlik.Ver.V_1_1
import static com.github.zhurlik.Ver.V_1_3

buildscript {
    repositories {
        maven {
            url uri('../tmp-repo')
        }
    }

    dependencies {
        classpath 'com.github.zhurlik:gradle-jboss-modules:0.18'
    }
}

apply plugin: 'com.github.zhurlik.jbossmodules'

repositories {
    mavenCentral()
}

configurations {
    // additional configuration because activemqra requires different version of spring
    activemqra
}

dependencies {
    ['web', 'context-support', 'jdbc', 'oxm', 'orm', 'jms', 'webmvc'].each {
        jbossmodules "org.springframework:spring-${it}:${springVersion}"
    }
    jbossmodules 'com.caucho:hessian:4.0.38'
    jbossmodules 'com.thoughtworks.xstream:xstream:1.4.7'
    jbossmodules 'org.apache.camel:camel-core:2.15.1'
    jbossmodules 'org.springframework:spring-struts:3.2.16.RELEASE'
    jbossmodules 'org.springframework.ws:spring-xml:2.2.4.RELEASE'
    jbossmodules project(path: ':custom-jar', configuration: 'jar1')
    jbossmodules project(path: ':custom-jar', configuration: 'jar2')
    jbossmodules project(':custom-jar')

    // Begin activeMq
    ['aop', 'beans', 'core', 'context', 'core', 'expression'].each {
        activemqra "org.springframework:spring-${it}:3.2.8.RELEASE"
    }
    ['broker', 'client', 'jms-pool', 'kahadb-store', 'openwire-legacy', 'pool', 'ra', 'spring'].each {
        activemqra group: 'org.apache.activemq', name: "activemq-${it}", version: '5.10.0'
    }
    activemqra group: 'org.apache.activemq.protobuf', name: 'activemq-protobuf', version: '1.1'
    activemqra group: 'aopalliance', name: 'aopalliance', version: '1.0'
    activemqra group: 'commons-pool', name: 'commons-pool', version: '1.6'
    activemqra group: 'org.fusesource.hawtbuf', name: 'hawtbuf', version: '1.10'
    activemqra group: 'org.apache.xbean', name: 'xbean-spring', version: '3.16'
    activemqra group: 'commons-net', name: 'commons-net', version: '3.3'
    activemqra group: 'commons-pool', name: 'commons-pool', version: '1.6'
    activemqra group: 'org.apache.geronimo.specs', name: 'geronimo-j2ee-management_1.1_spec', version: '1.0.1'
    activemqra group: 'log4j', name: 'log4j', version: '1.2.17'
    // End activeMq
}

jbossrepos {
    'jboss7.1.1' {
        version = V_1_1
    }
    'wildfly8.2' {
        version = V_1_3
    }
}

modules {
    aopalliance {
        moduleName = 'org.aopalliance'
        resources = ['aopalliance-1.0.jar']
    }

    xstream {
        moduleName = 'com.thoughtworks.xstream'
        resources = ['xstream-1.4.7.jar']
    }

    hessian {
        moduleName = 'com.caucho.hessian'
        resources = ['hessian-4.0.38.jar']
    }

    // springframework
    springCore {
        moduleName = 'org.springframework.core'
        resources = ["spring-core-${springVersion}.jar"]
        dependencies = ['javax.api',
                        'org.jboss.vfs',
                        'org.apache.commons.logging'
        ]
    }

    springBeans {
        moduleName = 'org.springframework.beans'
        resources = ["spring-beans-${springVersion}.jar"]
        dependencies = ['org.springframework.core',
                        'javax.api',
                        'org.apache.commons.logging'
        ]
    }

    springAop {
        moduleName = 'org.springframework.aop'
        resources = ["spring-aop-${springVersion}.jar"]
        dependencies = ['org.springframework.beans',
                        'org.springframework.core',
                        'javax.api', 'org.apache.commons.logging', 'org.aopalliance'
        ]
    }

    springExpression {
        moduleName = 'org.springframework.expression'
        resources = ["spring-expression-${springVersion}.jar"]
        dependencies = ['org.springframework.core']
    }

    springContextSupport {
        moduleName = 'org.springframework.context-support'
        resources = ["spring-context-support-${springVersion}.jar"]
    }

    springTx {
        moduleName = 'org.springframework.tx'
        resources = ["spring-tx-${springVersion}.jar"]
        dependencies = ['org.springframework.aop',
                        'org.springframework.beans',
                        'org.springframework.context',
                        'org.springframework.core',
                        'javax.api', 'javax.transaction.api', 'org.apache.commons.logging', 'org.aopalliance'
        ]
    }

    springJdbc {
        moduleName = 'org.springframework.jdbc'
        resources = ["spring-jdbc-${springVersion}.jar"]
        dependencies = ['org.springframework.beans',
                        'org.springframework.context',
                        'org.springframework.core',
                        'org.springframework.tx',
                        'javax.api', 'org.apache.commons.logging'
        ]
    }

    springOxm {
        moduleName = 'org.springframework.oxm'
        resources = ["spring-oxm-${springVersion}.jar"]
        dependencies = [
                'org.springframework.beans',
                'org.springframework.core',
                'javax.api', 'javax.xml.bind.api', 'org.apache.commons.logging', 'com.thoughtworks.xstream'
        ]
    }

    springJms {
        moduleName = 'org.springframework.jms'
        resources = ["spring-jms-${springVersion}.jar"]
        dependencies = [
                'org.springframework.aop',
                'org.springframework.beans',
                'org.springframework.context',
                'org.springframework.core',
                'org.springframework.oxm',
                'org.springframework.tx',
                'javax.jms.api',
                'org.apache.commons.logging'
        ]
    }

    springWeb {
        moduleName = 'org.springframework.web'
        resources = ["spring-web-${springVersion}.jar"]
        dependencies = [
                'org.springframework.aop',
                'org.springframework.beans',
                'org.springframework.context',
                'org.springframework.core',
                'org.springframework.oxm',
                [name: 'com.fasterxml.jackson.core.jackson-annotations', export: true],
                [name: 'com.fasterxml.jackson.core.jackson-core', export: true],
                [name: 'com.fasterxml.jackson.core.jackson-databind', export: true],
                [name: 'com.fasterxml.jackson.jaxrs.jackson-jaxrs-json-provider', export: true],
                'javax.api', 'javax.servlet.api', 'javax.xml.bind.api', 'org.apache.commons.logging',
                'com.caucho.hessian', 'org.aopalliance'
        ]
    }

    springOrm {
        moduleName = 'org.springframework.orm'
        resources = ["spring-orm-${springVersion}.jar"]
        dependencies = [
                'org.springframework.aop',
                'org.springframework.beans',
                'org.springframework.context',
                'org.springframework.core',
                'org.springframework.jdbc',
                'org.springframework.tx',
                'org.springframework.web',
                'javax.api', 'org.apache.commons.logging'
        ]
    }

    springMvc {
        moduleName = 'org.springframework.webmvc'
        resources = ["spring-webmvc-${springVersion}.jar"]
        dependencies = [
                'org.springframework.beans',
                'org.springframework.context',
                'org.springframework.context-support',
                'org.springframework.core',
                'org.springframework.expression',
                'org.springframework.oxm',
                'org.springframework.web',
                'org.apache.velocity',
                [name: 'com.fasterxml.jackson.core.jackson-annotations', export: true],
                [name: 'com.fasterxml.jackson.core.jackson-core', export: true],
                [name: 'com.fasterxml.jackson.core.jackson-databind', export: true],
                [name: 'com.fasterxml.jackson.jaxrs.jackson-jaxrs-json-provider', export: true],
                'javax.api', 'javax.servlet.api', 'org.apache.commons.logging', 'javax.servlet.jsp.api'
        ]
    }

    springStruts {
        moduleName = 'org.springframework.struts'
        resources = ["spring-struts-3.2.16.RELEASE.jar"]
        dependencies = [
                'org.springframework.beans',
                'org.springframework.context',
                'org.springframework.core',
                'org.springframework.web',
                'org.springframework.webmvc'
        ]
    }

    springXml {
        moduleName = 'org.springframework.xml'
        resources = ["spring-xml-2.2.4.RELEASE.jar"]
        dependencies = [
                'org.springframework.context',
                'org.springframework.core',
                'javax.api',
                'org.apache.commons.logging'
        ]
    }

    // springframework

    // Resource Adapter metadata files under src/${jboss-server}/dist/modules/${path-to-module}/
    // (in this case /src/wildfly8.2/dist/modules/org.apache/activemq/main/) will be copied
    // along with the module files define here into the distributions zip and tgz files.
    activemqra {
        // to define on which servers this module will be available, by default - all
        servers = ['wildfly8.2']
        moduleName = 'org.apache.activemq'
        resources = ['.',
                     'activemq-broker-5.10.0.jar',
                     'activemq-client-5.10.0.jar',
                     'activemq-jms-pool-5.10.0.jar',
                     'activemq-kahadb-store-5.10.0.jar',
                     'activemq-openwire-legacy-5.10.0.jar',
                     'activemq-pool-5.10.0.jar',
                     'activemq-protobuf-1.1.jar', 'activemq-ra-5.10.0.jar',
                     'activemq-spring-5.10.0.jar',
                     'aopalliance-1.0.jar',
                     'commons-pool-1.6.jar',
                     'commons-logging-1.1.3.jar',
                     'hawtbuf-1.10.jar',
                     'spring-aop-3.2.8.RELEASE.jar',
                     'spring-beans-3.2.8.RELEASE.jar',
                     'spring-context-3.2.8.RELEASE.jar',
                     'spring-core-3.2.8.RELEASE.jar',
                     'spring-expression-3.2.8.RELEASE.jar',
                     'xbean-spring-3.16.jar',
                     'commons-net-3.3.jar',
                     'commons-pool-1.6.jar',
                     'geronimo-j2ee-management_1.1_spec-1.0.1.jar',
                     'log4j-1.2.17.jar']
        exports = [exclude: ['org/springframework/**', 'org/apache/xbean/**', 'org/apache/commons/**', 'org/aopalliance/**', 'org/fusesource/**']]
        dependencies = ['javax.api', 'org.slf4j', 'javax.resource.api', 'javax.jms.api', 'javax.management.j2ee.api']

        configuration = configurations.activemqra
    }


    moduleA {
        // to define on which servers this module will be available, by default - all
        servers = ['serverA']
        moduleName = 'com.github.zhurlik.a'
        mainClass = 'zh'
        slot = '3.3.3'
        properties = ['ver' : '1.0', 'test' : 'zhurlik']
        resources = ['test1.jar', "spring-core-${springVersion}.jar",
                     [name: 'name', path: 'path1', filter: [include:'**']]
        ]
        dependencies = [
                [name: 'module1'],
                [name: 'module2', export: 'true'],
                [name: 'module3', export: 'false', exports: [
                        include: ['mine'],
                        exclude: ['*not*a', '*not*b']
                    ]
                ]
        ]
    }

    moduleB {
        moduleName = 'com.github.zhurlik.b'
        mainClass = 'zh'
        slot = '3.3.3'
        properties = ['ver' : '1.0', 'test' : 'zhurlik']
        resources = ['test1.jar', 'custom.*',
                     [name: 'name-jar2', path: 'test2.jar', filter: [exclude: '**']]
        ]
        dependencies = [
                [name: 'module1'],
                [name: 'module2', export: 'true'],
                [name: 'module3', export: 'false', imports: [
                        include: 'mine',
                        exclude: ['*not*a', '*not*b']
                    ]
                ]
        ]
    }
}

task initCamelModule(type: com.github.zhurlik.task.InitModuleTask) {
    pomName = 'camel-core-2.15.1'
}

//jbossrepos.each() {com.github.zhurlik.extension.JBossServer it->
//    println '>> Server:' + it.home + ' modules:\n'
//
//    it.initTree()
//    it.names.each {
//        println it
//    }
//
//    println it.getModule('org.jboss.jts.integration').moduleDescriptor
//    assert it.getModule('org.jboss.jts.integration').isValid()
//    println it.getMainXml('org.jboss.jts.integration')
//}

tasks.makeModules.dependsOn project(':custom-jar').tasks